program tithe;
{$I WaspLib/osr.simba}

begin
  Login.PlayerIndex := 0;
end;

const
  BREAKAFTER = 60;
  BREAKFOR   = 6;

var
  Seed: TRSItem;
  RSW: TRSWalker;
  Fruit: TRSItem;
  PlantTiles: TPointArray;
  WateringCan: TRSItem;
  StartXP, CurrentXP, GainedXP, WaterLimit, UsedWater: Int32;
  GrownColor, PlantColor: TCTS2Color;
  Timer, ReportTimer: TStopwatch;

procedure WriteDebug(Message: String);
begin
  WriteLn(SRL.TimeStamp(), ':[Script]: ', Message);
end;

function TRectangle.RandomMean(): TPoint;
var
  centerX, centerY, randX, randY: Double;
begin
  centerX := Self.Mean().X;
  centerY := Self.Mean().Y;

  randX := Self.Left.X + Random * (Self.Right.X - Self.Left.X);
  randY := Self.Top.Y + Random * (Self.Btm.Y - Self.Top.Y);

  Result.X := Trunc((centerX + randX) / 2);
  Result.Y := Trunc((centerY + randY) / 2);
end;

procedure PrintProgress();
var
  Hours: Double;
  Total, Hourly: String;
begin
  if ReportTimer.ElapsedTime() < 60000 then
    Exit();

  Hours := (GetTimeRunning()/3600000);
  Total := ToStr(Round((GainedXP/1000), 0), ' k ');
  Hourly := ToStr('(', Round((GainedXP/(Hours*1000)), 1), ' k / hr)');
  WriteLn(SRL.TimeStamp(), ':[Experience]: ', Total, Hourly);

  ReportTimer.Reset();
end;

procedure CalculateXP();
var
  TempValue: Int32;
begin
  CurrentXP := XPBar.Read();
  TempValue := GainedXP;
  GainedXP  := (CurrentXP - StartXP);

  if GainedXP > TempValue then
    Timer.Reset();

  if Timer.ElapsedTime() > 300000 then
    TerminateScript('No Gained XP in 5 mins, stopping!');
end;

procedure KeyboardRotate();
var
  a: Integer;
begin
  if SRL.Dice(50) then
    a := $25
  else
    a := $27;

  Keyboard.KeyDown(a);
  Sleep(SRL.NormalRange(250, 1550));
  Keyboard.KeyUp(a);
end;

procedure PauseTimer(Task: PBreakTask);
var
  t: PBreakTask;
begin
  Timer.Pause();
  t := Task;
end;

procedure ResumeTimer(Task: PBreakTask);
var
  t: PBreakTask;
begin
  Timer.Resume();
  t := Task;
end;

procedure TAntiban.DoLoseFocus();
begin
  Self.LoseFocus(SRL.NormalRange(450, 2750));
end;

procedure SetupAntiban();
begin
  Antiban.Skills := [ERSSKILL.FARMING, ERSSKILL.TOTAL];
  Antiban.AddTask(ONE_MINUTE * 2,  @Mouse.RandomMovement, 1);
  Antiban.AddTask(ONE_MINUTE * 10, @Antiban.HoverMSPlayers, 1);
  Antiban.AddTask(ONE_MINUTE * 10, @Antiban.DoLoseFocus, 1);
  Antiban.AddTask(ONE_MINUTE * 6,  @Antiban.RandomRotate, 1);
  Antiban.AddTask(ONE_MINUTE * 10, @KeyboardRotate, 1);
  Antiban.AddTask(ONE_MINUTE * 8,  @Antiban.HoverMSNPCs, 1);
  Antiban.AddTask(ONE_MINUTE * 25, @Antiban.HoverSkills, 1);
  Antiban.AddBreak(BREAKAFTER * ONE_MINUTE, BREAKFOR * ONE_MINUTE, 0.3, 1);
  Antiban.OnStartBreak := @PauseTimer;
  Antiban.OnFinishBreak := @ResumeTimer;
end;

procedure GeneratePlantArray();
var
  x, y: Int32;
  p: TPoint;
begin
  x := 2637;
  for 1 to 2 do
  begin
    y := 2467;
    for 1 to 4 do
    begin
      PlantTiles += Point(x, y);
      y += 12;
    end;
    x += 20;
  end;
end;

procedure DrawPlantArray();
var
  Tile: TRectangle;
  i: Int32;
begin
  for i := 0 to High(PlantTiles) do
  begin
    Tile := RSW.GetTileMS(PlantTiles[i]);
    RSClient.Image.DrawRect(Tile, clRed);
  end;
end;

function GetTileBounds(Tile: TPoint; Height, Expansion: Int32): TBox;
var
  Rect: TRectangle;
  Bds: TBox;
begin
  Rect := RSW.GetTileMs(Tile, Height);
  Bds := Rect.Bounds().Expand(Expansion);
  Bds.LimitTo(Mainscreen.Bounds());
  Result := Bds;
end;

function IsGrown(Tile: TPoint): Boolean;
var
  Bounds: TBox;
  Amount: Int32;
begin
  Bounds := GetTileBounds(Tile, 6, 2);
  Amount := SRL.CountColor(GrownColor, Bounds);
  Result := (Amount > 80);
end;

function IsDead(Tile: TPoint): Boolean;
var
  BlightedColor: TCTS2Color := CTS2(6186585, 12, 0.28, 0.27);
  Bounds: TBox;
  Amount: Int32;
begin
  Bounds := GetTileBounds(Tile, 0, 2);
  Amount := SRL.CountColor(BlightedColor, Bounds);
  Result := (Amount > 2);
end;

function ShouldFillCans(): Boolean;
begin
  Result := (UsedWater > WaterLimit);
end;

function IsFree(Tile: TPoint): Boolean;
var
  Bounds: TBox;
  Amount: Int32;
begin
  if IsDead(Tile) then
    Exit(False);

  Bounds := GetTileBounds(Tile, 0, 1);
  Amount := SRL.CountColor(PlantColor, Bounds);
  Result := (Amount < 1);
end;

function IsWatered(Tile: TPoint): Boolean;
var
  WaterColor: TCTS2Color := CTS2(5797500, 1, 0.01, 0.01);
  Amount: Int32;
  Bounds: TBox;
begin
  Bounds := GetTileBounds(Tile, 0, 2);
  Amount := SRL.CountColor(WaterColor, Bounds);
  Result := (Amount > 1);
end;

procedure FillWateringCans();
var
  BarrelTile: TPoint := [2629, 2459];
  Pos: TPoint := RSW.GetMyPos();
  BarrelRect: TRectangle;
begin
  if Pos.DistanceTo(BarrelTile) > 18 then
    RSW.WalkBlind(BarrelTile);

  BarrelRect := RSW.GetTileMs(BarrelTile);

  if not Mainscreen.Bounds.Contains(BarrelRect) then
    Exit();

  Inventory.ClickItem(WateringCan);
  Mouse.HumanMove(BarrelRect.RandomMean());

  if Mainscreen.IsUpText('Barrel') then
  begin
    Mouse.Click(MOUSE_LEFT);

    if WateringCan = 'Watering can' then
      WaitUntil(not Inventory.ContainsItem(WateringCan), 250, 180000)
    else
      WaitEx(8750, 1234);

    UsedWater := 0;

    Antiban.DoAntiban();
  end;
end;

function NeedsWatering(Tile: TPoint): Boolean;
begin
  if IsGrown(Tile) or IsFree(Tile) or IsDead(Tile) then
    Exit(False);

  Result := not IsWatered(Tile);
end;

procedure SetupGolovanova();
begin

end;

procedure SetupBologano();
begin
  WriteDebug('Setting up Bologano.');
  GrownColor := CTS2(6001531, 5, 0.26, 0.19);
  PlantColor := CTS2(6065729, 30, 0.54, 0.48);
  Fruit := 'Bologano fruit';
end;

procedure SetupLogavano();
begin

end;

procedure InitiateScript();
var
  Seeds: TRSItemArray := ['Golovanova seed', 'Bologano seed', 'Logavano seed'];
  CurSeed: TRSItem;
begin
  RSW.Setup([[2500, 2300, 2800, 2600]], 2);

  GeneratePlantArray();

  if not RSClient.IsLoggedIn() then
    Login.LoginPlayer();

  WaitUntil(RSClient.IsLoggedIn(), 250, 12000);
  StartXP := XPBar.Read();

  if StartXP < 69 then
    TerminateScript('Failed to read XP, make sure XPBar is set up!');

  Options.SetNPCAttackOption(ERSAttackOption.HIDDEN);
  Options.SetZoomLevel(Random(18, 22));

  for CurSeed in Seeds do
    if Inventory.ContainsItem(CurSeed) then
      Seed := CurSeed;

  case Seed of
    'Golovanova seed': SetupGolovanova();
    'Bologano seed': SetupBologano();
    'Logavano seed': SetupLogavano();
  end;

  WaterLimit := Random(140, 160);

  if Inventory.ContainsItem('Gricoller' + #39 + 's can') then
  begin
    WateringCan := 'Gricoller' + #39 + 's can';
    WaterLimit := 2 * WaterLimit;
  end else
    WateringCan := 'Watering can';

  Mouse.Speed := Random(19, 24);
  Mouse.Wind := Random(1, 3);
  RSW.AdaptiveWalk := True;

  SetupAntiban();

  Timer.Start();
  ReportTimer.Start();
end;

function GetClosestFreeSpot(Tiles: TPointArray): TPoint;
var
  i: Int32;
begin
  for i := 0 to High(Tiles) do
    if IsFree(Tiles[i]) then
      Exit(Tiles[i]);
end;

procedure WaterPlant(Tile: TPoint);
var
  Pt: TPoint;
  Rect: TRectangle;
begin
  if Mainscreen.IsUpText('>') then
    Mouse.Click(MOUSE_LEFT);

  Rect := RSW.GetTileMS(Tile);
  Pt := Rect.RandomMean();
  Mouse.HumanMove(Pt);

  if Mainscreen.IsUpText('Water', 225) then
  begin
    Mouse.Click(MOUSE_LEFT);
    WaitUntil(IsWatered(Tile), 250, 4575);
    Inc(UsedWater);
  end else
    Antiban.RandomRotate();
end;

procedure HarvestPlant(Tile: TPoint);
var
  Pt: TPoint;
  Rect: TRectangle;
  Fruits: Int32;
begin
  if Mainscreen.IsUpText('>') then
    Mouse.Click(MOUSE_LEFT);

  Fruits := Inventory.CountItemStack(Fruit);

  Rect := RSW.GetTileMS(Tile);
  Pt := Rect.RandomMean();
  Mouse.HumanMove(Pt);

  if Mainscreen.IsUpText('Harvest', 225) then
  begin
    Mouse.Click(MOUSE_LEFT);
    WaitUntil(Fruits < Inventory.CountItemStack(Fruit), 250, 4575);
  end else
    Antiban.RandomRotate();
end;

procedure Fertilise(Tile: TPoint);
var
  F: TRSItem := 'Gricoller' + #39 + 's fertiliser';
  Rect: TRectangle;
  Fruits: Int32;
  Bounds: TBox;
  Pt: TPoint;
begin
  if Mainscreen.IsUpText('>') then
    Mouse.Click(MOUSE_LEFT);

  Inventory.ClickItem(F);
  WaitEx(225, 200);

  Bounds := GetTileBounds(Tile, 0, 1);
  Rect := Bounds.ToRectangle();
  Pt := Rect.RandomMean();
  Mouse.HumanMove(Pt);

  if not Mainscreen.IsUpText('seedling', 225) then
    Exit();

  Mouse.Click(MOUSE_LEFT);
  WaitEx(1075, 125);
end;

procedure Plant(Tile: TPoint);
var
  Pt: TPoint;
  Rect: TRectangle;
  Bounds: TBox;
  Seeds: Int32;
begin
  if Mainscreen.IsUpText('>') then
    Mouse.Click(MOUSE_LEFT);

  Seeds := Inventory.CountItemStack(Seed);
  Inventory.ClickItem(Seed);

  Bounds := GetTileBounds(Tile, 0, 1);
  Rect := Bounds.ToRectangle();
  Pt := Rect.RandomMean();
  Mouse.HumanMove(Pt);

  if not Mainscreen.IsUpText('Tithe', 225) then
    Exit();

  Mouse.Click(MOUSE_LEFT);
  WaitUntil(Seeds > Inventory.CountItemStack(Seed), 250, 4575);
end;

procedure Farm();
var
  Plants: TPointArray;
  New, Pos: TPoint;
  i: Int32;
begin
  Pos := RSW.GetMyPos();
  Plants := PlantTiles.Sorted(Pos);

  for i := 0 to High(Plants) do
    if NeedsWatering(Plants[i]) then
    begin
      WriteDebug('Watering');
      WaterPlant(Plants[i]);
      Exit();
    end;

  for i := 0 to High(Plants) do
    if IsGrown(Plants[i]) then
    begin
      WriteDebug('Harvesting.');
      HarvestPlant(Plants[i]);
      Exit();
    end;

  if ShouldFillCans() then
  begin
    FillWateringCans();
    Exit();
  end;

  New := GetClosestFreeSpot(Plants);
  Plant(New);
  Fertilise(New);
end;

procedure Tithe();
begin
  Antiban.DoAntiban(False, False);

  if not RSClient.IsLoggedIn() then
    Login.LoginPlayer();

  CalculateXP();
  PrintProgress();
  Farm();

  WaitEx(255, 225);
end;

begin
  InitiateScript();
  repeat
    Tithe();
  until false;
end.