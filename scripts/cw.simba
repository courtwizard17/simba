program cw;
{$I WaspLib/osr.simba}

begin
  Login.PlayerIndex := 0;
end;

const
  BREAKAFTER  = 60;
  BREAKFOR    = 6;

type
  EState = (JOIN_GAME, EXIT_BASE, IDLE);
  ETeam = (SARADOMIN, ZAMORAK, UNKNOWN);

var
  RSW: TRSWalker;
  Portal, ZamorakLadder, SaradominLadder: TRSObject;

procedure DownloadMap(const URL, Filename: string);
var
  Client: Int32;
begin
  Client := InitializeHTTPClient(False);
  try
    GetHTTPPageEx(Client, URL, Filename);
  finally
    FreeHTTPClient(Client);
  end;
end;

procedure WriteDebug(Message: String);
begin
  WriteLn(SRL.TimeStamp(), ':[Script]: ', Message);
end;

function GetState(): EState;
var
  Pos: TPoint := RSW.GetMyPos();
begin
  if Pos.InBox([540, 176, 602, 261]) then
    Exit(JOIN_GAME);

  (* Zamorak base *)
  if Pos.InBox([999, 285, 1042, 327]) then
    Exit(EXIT_BASE);

  (* Saradomin base *)
  if Pos.InBox([1220, 505, 1261, 545]) then
    Exit(EXIT_BASE);

  Exit(IDLE);
end;

procedure TAntiban.DoLoseFocus();
begin
  Self.LoseFocus(SRL.NormalRange(1250, 2750));
end;

procedure SetAntiban();
begin
  Antiban.AddTask(ONE_MINUTE * 2, @Antiban.RandomMouse, 1);
  Antiban.AddTask(ONE_MINUTE * 4, @Antiban.RandomRotate, 1);
  Antiban.AddTask(ONE_MINUTE * 3, @Antiban.DoLoseFocus, 1);
  Antiban.AddTask(ONE_MINUTE * 8, @Antiban.HoverMSNPCs, 1);
  Antiban.AddBreak(BREAKAFTER * ONE_MINUTE, BREAKFOR * ONE_MINUTE, 0.2, 1);
end;

procedure JoinGame();
var
  T: Int32;
begin
  WriteDebug('In Lobby');

  T := Inventory.CountItemStack('Castle wars ticket');
  WriteDebug(ToStr('Current tickets: ', T));

  Antiban.DoAntiban();

  if Portal.Click() then
    WaitUntil(GetState() <> JOIN_GAME, 250, 5000)
  else
    Antiban.RandomRotate();
end;

function GetTeam(): ETeam;
var
  Pos: TPoint := RSW.GetMyPos();
begin
  if Pos.InBox([928, 88, 1168, 413]) then
    Exit(ZAMORAK);

  if Pos.InBox([1183, 94, 1349, 592]) then
    Exit(SARADOMIN);

  WriteDebug('Unknown team!');
  Exit(UNKNOWN);
end;

procedure ExitBase();
var
  Team: ETeam := GetTeam();
begin
  WriteDebug(ToStr('Trying to exit base: ', Team));

  case Team of
    SARADOMIN: SaradominLadder.Click();
    ZAMORAK: ZamorakLadder.Click();
  end;

  if not WaitUntil(GetState() <> EXIT_BASE, 250, 12750) then
    WriteDebug('Failed to exit base!');
end;

procedure Loop();
begin
  if not RSClient.IsLoggedIn() then
    Login.LoginPlayer();

  case GetState() of
    EXIT_BASE: ExitBase();
    JOIN_GAME: JoinGame();
    IDLE: Wait(1337);
  end;

  WaitEx(1750, 555);
  Antiban.DoAntiban(False, False);
end;

procedure Init();
var
  MapURL: String := 'https://i.imgur.com/iP5YwqI.png';
begin
  if not FileExists(AppPath+'\Includes\WaspLib\osr\walker\maps\castle_wars.png') then
    DownloadMap(MapURL, AppPath+'\Includes\WaspLib\osr\walker\maps\castle_wars.png');

  RSW.Setup('castle_wars');

  if not RSClient.IsLoggedIn() then
    Login.LoginPlayer();

  WaitUntil(RSClient.IsLoggedIn(), 100, 10000);

  SetAntiban();

  with SaradominLadder do
  begin
    Setup(4, [[1248, 534]]);
    SetupUpText(['Ladder', 'Climb']);
    Finder.Colors += CTS2(1197664, 11, 0.11, 2.91);
  end;

  with ZamorakLadder do
  begin
    Setup(4, [[1012, 298]]);
    SetupUpText(['Ladder', 'Climb']);
    Finder.Colors += CTS2(1197664, 11, 0.11, 2.91);
  end;

  with Portal do
  begin
    Setup(2, [[552, 220]]);
    SetupUpText(['er G', 'Guthi']);
    Finder.Colors += CTS2(7649642, 11, 0.55, 1.72);
  end;

  if not InRange(Options.GetZoomLevel(), 0, 10) then
    Options.SetZoomLevel(Random(0, 5));
end;

begin
  Init();

  repeat
    Loop();
  until false;
end.
