program pickpocketer;
{$I WaspLib/osr.simba}
{$I chores.simba}

type
  EState = (PICK_POCKET, EAT_FOOD, RE_STOCK);

  TGUI = record (TScriptForm)
    BlockInputCheckbox, BirdhouseCheckbox: TLabeledCheckBox;
    BreakForInput, BreakAfterInput: TLabeledEdit;
    FoodAmountInput: TLabeledEdit;
    FoodCombobox: TLabeledCombobox;
  end;

var
  StartXP, CurrentXP, GainedXP, MinHP, BreakFor, BreakAfter, FoodAmount: Int32;
  Birdhouses, UsingPouches, BlockInput: Boolean;
  InactivityTimer, ReportTimer: TStopwatch;
  ThievArea, BankArea: TPointArray;
  ReturnSpell: ERSSpell;
  UpTexts: TStringArray;
  Runes: TRSItemArray;
  Booth: TRSObject;
  CityArea: TBox;
  RSW: TRSWalker;
  Food: TRSItem;
  GUI: TGUI;

procedure TGUI.StartScript(Sender: TObject); override;
begin
  BreakFor := Self.BreakForInput.GetText().ExtractInteger();
  BlockInput := Self.BlockInputCheckbox.IsChecked();
  BreakAfter := Self.BreakAfterInput.GetText().ExtractInteger();
  Birdhouses := Self.BirdhouseCheckbox.IsChecked();
  FoodAmount := Self.FoodAmountInput.GetText().ExtractInteger();
  Food := Self.FoodCombobox.GetText();

  inherited;
end;

procedure TGUI.Run(); override;
var
  Tab: TTabSheet;
  Font: TFont;
begin
  Self.Setup('Pickpocketer', Point(800, 500), False);
  Self.Start.SetOnClick(@Self.StartScript);

  Font.Init();
  Font.setName('Verdana');
  Font.SetQuality(fqCleartype);
  Self.Form.setFont(Font);
  Self.PageControl.SetFont(Font);
  Font.Free();

  Self.AddTab('Settings');

  Tab := Self.GetTab('Settings');
  Tab.SetTooltip('');

  with Self.FoodCombobox do
  begin
    Create(Tab);
    SetCaption('Food');
    SetLeft(TControl.AdjustToDPI(470));
    SetTop(TControl.AdjustToDPI(160));
    AddItemArray(["Stew", "Lobster", "Shark", "Manta ray",
                  "Bass", "Cooked karambwan"]);
    SetItemIndex(4);
  end;

  with Self.FoodAmountInput do
  begin
    Create(Tab);
    SetLeft(TControl.AdjustToDPI(470));
    SetTop(TControl.AdjustToDPI(210));
    SetToolTip('How many food to withdraw from bank?');
    SetCaption('Food amount:');
    SetText('6');
  end;

  with Self.BreakAfterInput do
  begin
    Create(Tab);
    SetLeft(TControl.AdjustToDPI(30));
    SetTop(TControl.AdjustToDPI(160));
    SetCaption('Break after');
    SetText('45');
  end;

  with Self.BlockInputCheckbox do
  begin
    Create(Tab);
    SetCaption('Block real input');
    SetLeft(TControl.AdjustToDPI(30));
    SetTop(TControl.AdjustToDPI(280));
  end;

  with Self.BreakForInput do
  begin
    Create(Tab);
    SetLeft(TControl.AdjustToDPI(30));
    SetTop(TControl.AdjustToDPI(220));
    SetCaption('Break for');
    SetText('5');
  end;

  with Self.BirdhouseCheckbox do
  begin
    Create(Tab);
    SetCaption('Do birdhouse runs');
    SetLeft(TControl.AdjustToDPI(30));
    SetTop(TControl.AdjustToDPI(310));
  end;

  Self.CreateAccountManager(Tab);
  Self.CreateBirdhouseRunSettings();

  inherited;
end;

procedure OnBreakEnd(Task: PBreakTask);
var
  T: PBreakTask;
begin
  InactivityTimer.Resume();
  T := Task;
end;

procedure TAntiban.DoLoseFocus();
begin
  Self.LoseFocus(SRL.NormalRange(450, 2750));
end;

procedure WriteDebug(Message: String);
begin
  WriteLn(SRL.TimeStamp(), ':[Script]: ', Message);
end;

function HasGainedXP(): Boolean;
var
  TempValue: Int32;
begin
  CurrentXP := XPBar.Read();
  TempValue := GainedXP;
  GainedXP  := CurrentXP - StartXP;
  Result := GainedXP > TempValue;
end;

procedure PrintProgress();
var
  Hours: Double;
  Total, Hourly: String;
begin
  Hours := GetTimeRunning()/3600000;
  Total := ToStr(Round((GainedXP/1000), 0), ' k ');
  Hourly := ToStr('(', Round((GainedXP/(Hours*1000)), 1), ' k / hr)');
  WriteLn(SRL.TimeStamp(), ':[Experience]: ', Total, Hourly);
  ReportTimer.Reset();
end;

procedure OnBreakStart(Task: PBreakTask);
var
  T: PBreakTask;
begin
  InactivityTimer.Pause();
  T := Task;
end;

procedure SetupAntiban();
begin
  Antiban.AddTask(ONE_MINUTE * 4, @Antiban.RandomRightClick, 1);
  Antiban.AddTask(ONE_MINUTE * 2, @Antiban.RandomMouse, 1);
  Antiban.AddTask(ONE_MINUTE * 5, @Antiban.RandomRotate, 1);
  Antiban.AddTask(ONE_MINUTE * 4, @Antiban.DoLoseFocus, 1);

  Antiban.AddBreak(ONE_MINUTE * 6, ONE_SECOND * 5, 1, 0);
  Antiban.AddBreak(BREAKAFTER * ONE_MINUTE, BREAKFOR * ONE_MINUTE, 0.3, 1);

  Antiban.OnStartBreak := @OnBreakStart;
  Antiban.OnFinishBreak := @OnBreakEnd;
end;

procedure Init();
var
  DraynorMarketArea: TPointArray := [[7686, 3425], [7740, 3428], [7741, 3472], [7685, 3473]];
  ArdougneBank: TPointArray := [[5987, 3297], [6015, 3297], [6015, 3330], [5987, 3330]];
  DraynorBank: TPointArray := [[7754, 3463], [7784, 3462], [7784, 3490], [7754, 3489]];
  ArdougneCityArea: TBox := [5888, 3166, 6112, 3391];
  DraynorCityArea: TBox := [7700, 3200, 8500, 3800];
  ArdougneBankBoothTile: TPoint := [6016, 3304];
  DraynorBankBoothTile: TPoint := [7758, 3468];
  Pos: TPoint;
begin
  if Birdhouses then
    BirdHouseRunner.Init();

  Mouse.Speed := Random(10, 15);
  Mouse.Gravity := Mouse.Gravity * 3;
  Mouse.Speed := 2 * Mouse.Speed;
  Mouse.Wind := Mouse.Wind / 3;
  Mouse.CanIdle := False;
  Mouse.MissChance := 0;

  UpTexts += ['Pickpoc', 'cket'];

  RSW.Setup();
  SetupAntiban();

  if not RSClient.IsLoggedIn() then
    Login.LoginPlayer();

  WaitUntil(RSClient.IsLoggedIn(), 250, 12000);

  StartXP := XPBar.Read();

  if StartXP < 1 then
    TerminateScript('Failed to read XP, make sure XPBar is set up!');

  if Options.GetNPCAttackOption() <> ERSAttackOption.HIDDEN then
    Options.SetNPCAttackOption(ERSAttackOption.HIDDEN);

  if not InRange(Options.GetZoomLevel(), 10, 25) then
    Options.SetZoomLevel(Random(10, 25));

  MinHP := Floor(Minimap.GetHPLevel() * 0.7);
  WriteDebug(ToStr('Will eat at HP: ', MinHP));

  Pos := RSW.GetMyPos();

  if ArdougneCityArea.Contains(Pos) then
  begin
    WriteDebug('Setting up at Ardougne Knights.');
    UpTexts += ['night', 'Ardougn'];
    BankArea := ArdougneBank;
    ThievArea := ArdougneBank;
    CityArea := ArdougneCityArea;
    UsingPouches := True;
    Booth.SetupUpText(['Bank','Bank booth']);
    Booth.Setup(6, [ArdougneBankBoothTile]);
    Booth.Finder.Colors += CTS2(6251367, 1, 1.67, 1.48);
    Runes := ['Law rune', 'Water rune'];
    ReturnSpell := ERSSpell.ARDOUGNE_TELEPORT;
  end
  else if DraynorCityArea.Contains(Pos) then
  begin
    WriteDebug('Setting up at Draynor farmer.');
    UpTexts += ['armer', 'aster'];
    BankArea := DraynorBank;
    ThievArea := DraynorMarketArea;
    CityArea := DraynorCityArea;
    Booth.SetupUpText(['Bank','Bank booth']);
    Booth.Setup(6, [DraynorBankBoothTile]);
    Booth.Finder.Colors += CTS2(6251367, 1, 1.67, 1.48);
    Runes := ['Air rune', 'Law rune', 'Earth rune'];
    ReturnSpell := ERSSpell.LUMBRIDGE_TELEPORT;
  end;

  InactivityTimer.Start();
  ReportTimer.Start();
end;

function ShouldRestock(): Boolean;
begin
  if not Inventory.IsOpen() then
    Inventory.Open();

  if Inventory.CountEmptySlots() < 2 then
    Exit(True);

  if not Inventory.ContainsItem(Food) then
    Exit(True);
end;

procedure Restock();
var
  Item: TRSBankItem := [Food, FOODAMOUNT, False];
begin
  if not Bank.IsOpen() then
    if Booth.WalkClick(True, 3) then
      WaitUntil(Bank.IsOpen(), 250, 5750);

  if not Bank.IsOpen() then
    Exit();

  if Bank.DepositAll() then
    WaitEx(500, 495);

  if Bank.WithdrawItem(Item, False) then
    WaitEx(500, 495);

  if Bank.Close(60) then
    WaitEx(1500, 1495);
end;

procedure EatFood();
begin
  if not Inventory.ClickItem(Food, 'Eat') then
  begin
    WriteDebug('Failed to eat food!');

    if SRL.Dice(33) then
      Antiban.RandomRotate();

    Exit();
  end else
    WaitEx(2250, 325)
end;

procedure Steal();
var
  Experience: Int32;
begin
  Experience := XPBar.Read();

  Mouse.Click(MOUSE_LEFT);

  if Mainscreen.DidRedClick() then
    WaitUntil(Experience < XPBar.Read(), 250, SRL.NormalRange(4500, 4750));
end;

procedure Pickpocket();
var
  TPA: TPointArray;
begin
  if Mainscreen.IsUpText(UpTexts) then
  begin
    Steal();
    Exit();
  end;

  if not RSW.InPoly(ThievArea) then
    RSW.WebWalk(ThievArea.Mean());

  SRL.FindColors(TPA, CTS2(16711935, 1, 0.01, 0.01), Mainscreen.Bounds);

  if TPA.Len() = 0 then
  begin
    if InactivityTimer.ElapsedTime() > 45000 then
      Antiban.RandomRotate();

    Exit();
  end else
    Mouse.HumanMove(TPA.Mean());

  if Mainscreen.IsUpText(UpTexts) then
    Steal();
end;

procedure HandlePouches();
var
  Item: TRSItem := 'Coin pouch';
  Pouches: Int32;
begin
  Pouches := Inventory.CountItemStack(Item);

  if Pouches < 5 then
    Exit();

  if Pouches < 20 then
    if SRL.Dice(95) then
      Exit();

  Inventory.ClickItem(Item);
end;

function GetState(): EState;
begin
  if Minimap.GetHPLevel() < MinHP then
    Exit(EAT_FOOD);

  if ShouldRestock() then
    Exit(RE_STOCK);

  Exit(PICK_POCKET);
end;

procedure Loop();
var
  State: EState;
begin
  if InactivityTimer.ElapsedTime() > 300000 then
    TerminateScript('No XP gained in 5 minutes.');

  Antiban.DoAntiban();

  if not RSClient.IsLoggedIn() then
    Login.LoginPlayer();

  if RSInterface.IsOpen() then
    RSInterface.Close();

  if HasGainedXP() then
    InactivityTimer.Reset();

  if ReportTimer.ElapsedTime() > 120000 then
    PrintProgress();

  if BIRDHOUSES then
    if BirdHouseRunner.CanDoBirdHouseRun() then
      BirdHouseRunner.DoBirdHouseRun();

  if UsingPouches then
    HandlePouches();

  State := GetState();

  if State <> EState.PICK_POCKET then
    WriteDebug(ToStr(State));

  case State of
    ESTate.EAT_FOOD: EatFood();
    EState.PICK_POCKET: Pickpocket();
    EState.RE_STOCK: Restock();
  end;

  WaitEx(695, 690);
end;

function TBirdHouseInclude.OnStart(): Boolean; override;
var
  BankTimeout: TCountdown;
  Item: TRSBankItem;
  i, j: Int32;
begin
  WriteDebug('Starting a Birdhouse Run');

  for i := 0 to High(Antiban.Tasks) do
    Antiban.Tasks[i].Countdown.Pause();

  if Inventory.ContainsItem(Food) then
    Inventory.ClickItem(Food);

  BankTimeout.Init(55000);

  while not BankTimeOut.IsFinished() and not Bank.IsOpen() do
    if Booth.WalkClick() then
      WaitUntil(Bank.IsOpen(), 750, 12500);

  if not Bank.IsOpen() then
    Exit(False);

  if Bank.DepositAll() then
    if WaitUntil(Inventory.CountEmptySlots() > 25, 500, 2000) then
      WaitEx(750, 500);

  if not Self.WithdrawNextRun() then
  begin
    Self.DebugLn("Failed to get bank items");
    Bank.DepositAll();
    Bank.Close();
    BirdhouseRunner.IsDisabled := True;
    Exit(False);
  end;

  for j := 0 to High(Runes) do
  begin
    Item := TRSBankItem.Setup(Runes[j], 10, False);
    Bank.WithdrawItem(Item, False);
    WaitEx(800, 450);
  end;

  Result := Bank.Close(60);
end;

procedure TBirdHouseInclude.OnComplete(); override;
var
  i: Int32;
begin
  WriteDebug('Birdhouse run completed');

  if Magic.CastSpell(ReturnSpell) then
    WaitEx(5750, 750);

  for i := 0 to High(Antiban.Tasks) do
    Antiban.Tasks[i].Countdown.Resume();
end;

begin
  GUI.Run();
  Init();

  repeat
    Loop();
  until False;
end.
